plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation project(path: ':opencv')
    // opencv를 직접 프로젝트에 담았고, 그 안에도 있기 때문에 배포파일 만들때 충돌남
    // implementation 'com.websitebeaver:opencv:4.5.5'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'

    // text recognition
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.7.10'
    implementation 'androidx.multidex:multidex:2.0.1'

    // Text features
    implementation 'com.google.mlkit:text-recognition:16.0.0-beta6'
    // Or comment the dependency above and uncomment the dependency below to
    // use unbundled model that depends on Google Play Services
    // implementation 'com.google.android.gms:play-services-mlkit-text-recognition:18.0.2'
    implementation 'com.google.mlkit:text-recognition-chinese:16.0.0-beta6'
    implementation 'com.google.mlkit:text-recognition-devanagari:16.0.0-beta6'
    implementation 'com.google.mlkit:text-recognition-japanese:16.0.0-beta6'
    implementation 'com.google.mlkit:text-recognition-korean:16.0.0-beta6'

    implementation 'com.google.mlkit:camera:16.0.0-beta3'

    // Object detection feature with bundled default classifier
    implementation 'com.google.mlkit:object-detection:17.0.0'

    // Object detection feature with custom classifier support
    implementation 'com.google.mlkit:object-detection-custom:17.0.0'

    // ------------------------------------------------

    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.google.guava:guava:27.1-android'

    // Core library
    androidTestImplementation 'androidx.test:core:1.5.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.5.1'
    androidTestImplementation 'androidx.test:rules:1.5.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.5.1"

    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.annotation:annotation:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // CameraX
    implementation "androidx.camera:camera-camera2:1.2.0-rc01"
    implementation "androidx.camera:camera-lifecycle:1.2.0-rc01"
    implementation "androidx.camera:camera-view:1.2.0-rc01"

   // On Device Machine Learnings
    implementation "com.google.android.odml:image:1.0.0-beta1"
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = "com.websitebeaver"
            artifactId = "documentscanner"
            version = "1.0.0"

            afterEvaluate {
                from components.release

                pom {
                    name = 'Document Scanner'
                    description = 'Document Scanner'
                    url = 'https://github.com/websitebeaver/android-document-scanner'
                    licenses {
                        license {
                            name = 'The MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'dmarcs'
                            name = 'David Marcus'
                            email = 'dmarcus@websitebeaver.com'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com:websitebeaver/android-document-scanner.git'
                        developerConnection = 'scm:git@github.com:websitebeaver/android-document-scanner.git'
                        url = 'https://github.com/websitebeaver/android-document-scanner'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "mavencentral"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username findProperty("ossrhUsername")
                password findProperty("ossrhPassword")
            }
            // Depending on AndroidX Snapshot Builds to get the latest CameraX libs.
            url 'https://androidx.dev/snapshots/builds/6787662/artifacts/repository/'
        }
    }
}

signing {
    def signingKey = findProperty("ossrhSigningKey")
    def signingPassword = findProperty("ossrhSigningPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.release
}